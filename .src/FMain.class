' Gambas class file

sExtList As New String[]
bExt As Boolean
sFileStore As New String[]
bStop As Boolean
bFirst As Boolean
bExtOpen As Boolean                                                           'Is the Extension Manager window open
'sHasFolderChanged As String

Public Sub Form_Open()

DirChooserFF.SelectedPath = Settings["SelectedPath", User.Home]
TextBoxSearch.SetFocus
Settings.Read(Me, "Window")
bFirst = True

End

Public Sub DirChooserFF_Change()
Dim sFolder As String = DirChooserFF.SelectedPath
Dim sTemp As String
Dim sStat As Stat
Dim sTempStore As New String[]
Dim sBuildStore As New String[]
Dim bNoExt As Boolean
Dim byHidden As Byte = 2

If CheckBoxHidden.Value = True Then byHidden = 1
If bFirst = False Then Return
ButtonView.Enabled = True
Settings["SelectedPath"] = sFolder
Settings.Save
sFileStore.Clear
TimerWorking.Start
Wait 0.1

If DirChooserFF.SelectedPath = "/home" Or DirChooserFF.SelectedPath = User.Home Or DirChooserFF.SelectedPath = "/" Then
  CheckBoxSubFolders.Value = 0
  CheckBoxSubFolders.Enabled = False
Else
  CheckBoxSubFolders.Enabled = True
Endif

If Not bExt Then
  If CheckBoxSubFolders.value = True Then
    Try sTempStore = RDir(sFolder, "*", gb.File) 
  Else
    Try sTempStore = Dir(sFolder, "*", gb.File)
  End If
  For Each sTemp In sTempStore
    Try sStat = Stat(sFolder &/ sTemp)
    If sStat.Hidden = False Then sFileStore.Add(sTemp)
    If CheckBoxHidden.value = True And sStat.Hidden = True Then sFileStore.Add(sTemp)
  Next
Else
  
  For Each sTemp In sExtList
    If sTemp = "NO EXT" Then
      bNoExt = True
      Continue
    Endif
    If CheckBoxSubFolders.value = True Then
      sBuildStore = RDir(sFolder, "*." & sTemp, gb.File) 
    Else
      sBuildStore = Dir(sFolder, "*." & sTemp, gb.File)
    End If
    sTempStore.Insert(sBuildStore)
    sBuildStore.Clear
  Next
  
  For Each sTemp In sTempStore
    sStat = Stat(sFolder &/ sTemp)
    If sStat.Hidden = False Then sFileStore.Add(sTemp)
    If CheckBoxHidden.value = True And sStat.Hidden = True Then sFileStore.Add(sTemp)
  Next
  
  If bNoExt Then 
    sBuildStore = GetNoExtFiles()
    sFileStore.Insert(sBuildStore)
  End If
  
End If

sFileStore.sort()

ValueBoxNoFiles.Value = sFileStore.Count
If sFileStore.Count > 5000 Then ButtonView.Enabled = False
LabelWorking.Visible = False
TextBoxSearch.SetFocus
TimerWorking.Stop
Wait 0.1

End

Public Sub GetNoExtFiles() As String[]
Dim sFolder As String = DirChooserFF.SelectedPath
Dim sBuildStore, sRetunStore As New String[]
Dim sTemp As String
Dim sStat As Stat

'If sFolder <> sHasFolderChanged Then bExt = False

If CheckBoxSubFolders.value = True Then
  sBuildStore = RDir(sFolder, "*", gb.File) 
Else
  sBuildStore = Dir(sFolder, "*", gb.File)
End If

For Each sTemp In sBuildStore
  sStat = Stat(sFolder &/ sTemp)
  If File.Ext(sFolder &/ sTemp) = "" And sStat.Hidden = False Then sRetunStore.Add(sTemp)
  If File.Ext(sFolder &/ sTemp) = "" And CheckBoxHidden.value = True And sStat.Hidden = True Then sRetunStore.Add(sTemp)
Next

If CheckBoxHidden.value = True Then
  For Each sTemp In sBuildStore
    If sTemp Not Begins "." Then Continue
    If File.ext(sFolder &/ sTemp) And File.BaseName(sFolder &/ sTemp) = "" Then sRetunStore.Add(sTemp)
  Next
End If

Return sRetunStore

End

Public Sub NoExtention() As String[]
Dim sFolder As String = DirChooserFF.SelectedPath
Dim sList As New String[] 
Dim sOutput As New String[]
Dim sTemp As String

If CheckBoxSubFolders.Value = True Then
  sList = RDir(sFolder, "*", gb.file)
Else
  sList = Dir(sFolder, "*", gb.file)
Endif

For Each sTemp In sList
  If Not InStr(sTemp, ".") Then sOutput.Add(sTemp)
Next

Return sOutput

End

Public Sub TimerWorking_Timer()

LabelWorking.Visible = True
Wait 0.1

End

Public Sub ValueBoxNoFiles_Change()

TextBoxSearch_Change

End

Public Sub TextBoxSearch_Change()

ButtonSearch.Enabled = False

If Trim(TextBoxSearch.Text) = "" Then 
  ButtonSearch.Enabled = False
Else If ValueBoxNoFiles.Value > 0
  ButtonSearch.Enabled = True
Endif

End

Public Sub AllButtons_Click(Optional sDirectInput As String)
Dim sName As String = sDirectInput
Dim bTemp As Boolean

If sName = "" Then sName = Last.name

Select Case sName
  Case "ButtonView"
    ViewFiles(ButtonView.Value)
  Case "ButtonExit"
    Me.Close
  Case "ButtonAbout"
    DisplayAbout(ButtonAbout.Value)
  Case "ButtonSearch"
    DisplaySearch(ButtonSearch.value)
    If ButtonSearch.value = True Then DoTheSearch
  Case "ButtonStop"
    bStop = True
  Case "ButtonExtensions"
    bTemp = bExt
    bExt = False
    DirChooserFF_Change
    bExt = bTemp
    Extensions
End Select

TextBoxSearch.SetFocus

Catch

End

Public Sub Extensions()
Dim sFolder As String = DirChooserFF.SelectedPath
Dim sTemp, sTempExt, sExt As String
Dim sExtHold As String = "."
Dim hCheckBox As CheckBox
Dim siCount As Short

'sHasFolderChanged = sFolder

sExtHold &= sExtList.join(",")

bExtOpen = True

HBoxDir.Visible = False
HBox0.Visible = False
HBox1.Visible = False
HBoxExtensions.Visible = True
ButtonAbout.Enabled = False
ButtonSearch.Enabled = False

sExtList.Clear

For Each sTemp In sFileStore
  sExt = File.Ext(sFolder &/ sTemp)
  If sExt = "" Then sExt = "NO EXT"
  If Not InStr(sTempExt, sExt) Then sTempExt &= sExt & ","
Next

For Each sTemp In Split(sTempExt)
  If Not sTemp Then Continue
  sExtList.Add(sTemp)
Next

sExtList.Sort()
VPanelExtList.Children.Clear

For Each sTemp In sExtList
  hCheckBox = New CheckBox(VPanelExtList) As "AllExtCheckBoxes"
  With hCheckBox
    .Text = sTemp
    .Height = 28
    .AutoResize = True
    .Tag = siCount
  End With
  If sExtHold = "." Then 
    hCheckBox.value = True
  Else
    hCheckBox.value = InStr(sExtHold, sTemp)
  End If
  If sTemp = "NO EXT" Then hCheckBox.Tooltip = "This represents files with no extention"
  Inc siCount
Next

TimerExt.Trigger

End

Public Sub TimerExt_Timer()

Settings.Write(Me, "Window")                                                  'Store the window position and size
Settings.Save
Me.Width = 350 + VPanelExtList.Width

End

Public Sub BuildList()
Dim oObj As Object
Dim sNewList As New String[]

bExt = False

For Each oObj In VPanelExtList.Children
  If oObj.Value = True Then sNewList.add(oObj.Text)
  If oObj.Value = False Then bExt = True
Next

If bExt = True Then 
  sExtList = sNewList.Copy()
End If


End

Public Sub AllExtCheckBoxes_Click()
Dim oObj As Object

ButtonDone.Enabled = False

For Each oObj In VPanelExtList.Children
  If oObj.Value = True Then
    ButtonDone.Enabled = True
    Break
  End If 
Next

End

Public Sub AllSelect_Click()
Dim bOnOff As Boolean
Dim oObj As Object

If Last.Name = "ButtonSelectAll" Then bOnOff = True

For Each oObj In VPanelExtList.Children
  oObj.Value = bOnOff
Next

End

Public Sub ButtonsControl_Click()

If Last.Name = "ButtonDone" Then BuildList

HBoxDir.Visible = True
HBox0.Visible = True
HBox1.Visible = True
HBoxExtensions.Visible = False 
ButtonAbout.Enabled = True

Settings.Read(Me, "Window")                                                  'Store the window position and size

TextBoxSearch_Change
DirChooserFF_Change

bExtOpen = False

End

Public Sub DoTheSearch()
Dim sFolder As String = DirChooserFF.SelectedPath
Dim sFileData, sTemp As String
Dim sToFind As String = UCase(Trim(TextBoxSearch.Text))
Dim iRows, iCounter As Integer
Dim dSearchTime As Date
Dim dStartTime As Date = Time(Now)

HBoxProgress.Visible = True
ButtonExit.Enabled = False
ButtonSearch.Enabled = False
ButtonStop.Enabled = True
LabelFilesFound.Text = "Files found = " & Str(iRows)
LabelTime.text = ""

With GridViewFound
  .Rows.Height = 18
  .Clear
  .Columns.count = 1
End With

TextAreaFilesProcessed.text = ""

Wait 0.01

For Each sTemp In sFileStore
  Inc iCounter
  If bStop Then Break
  PanelProgress.Width = ((HBoxProgress.Width / 100) * (iCounter / sFileStore.max) * 100) - 4
  TextAreaFilesProcessed.text &= sTemp & gb.NewLine
  Try sFileData = UCase(File.Load(sFolder &/ sTemp))
  If InStr(sFileData, sToFind) Then
    Inc iRows
    LabelFilesFound.Text = "Files found = " & Str(iRows) 
    GridViewFound.Rows.count = iRows
    GridViewFound[iRows - 1, 0].text = sTemp
  Endif
  LabelSearchText.Text = "Search string = " & TextBoxSearch.Text
  LabelFilesSearched.Text = "Processed file " & Str(iCounter) & " of " & sFileStore.Count
  LabelFile.Text = sFolder &/ sTemp
  Wait 0.001
Next

HBoxProgress.Visible = False
If iRows > 0 Then LabelHelp.text = "Click on a file to open"
LabelFile.Text = ""
dSearchTime = Time(Now)
LabelTime.text = FormatTime(dStartTime)

bStop = False
ButtonExit.Enabled = True
ButtonSearch.Enabled = True
ButtonStop.Enabled = False

Catch

End

Public Sub GridViewFound_Click()
Dim sFolder As String = DirChooserFF.SelectedPath

Desktop.Open(sFolder &/ GridViewFound[Last.Row, Last.Column].Text)

End

Public Sub DisplaySearch(bMode As Boolean)

If bMode Then
  ButtonSearch.Tag = 1
  HBoxDir.Visible = False
  PanelProgress.Width = 1
  VBoxSearch.Visible = True
  HSplitSearch.Layout = [50, 50]
  HBox0.Visible = False
  HBox1.Visible = False
  ButtonAbout.Enabled = False
  ButtonSearch.Text = "C&lose"
Else
  ButtonSearch.Tag = 0
  HBoxDir.Visible = True
  VBoxSearch.Visible = False
  HBox0.Visible = True
  HBox1.Visible = True
  ButtonAbout.Enabled = True
  ButtonSearch.Text = "&Search"
Endif

End

Public Sub DisplayAbout(bMode As Boolean)

If bMode Then
  ButtonAbout.Tag = 1
  HBoxDir.Visible = False
  HBoxAbout.Visible = True
  HBox0.Visible = False
  HBox1.Visible = False
  ButtonSearch.Enabled = False
  ButtonAbout.Text = "C&lose"
Else
  ButtonAbout.Tag = 0
  HBoxDir.Visible = True
  HBoxAbout.Visible = False
  HBox0.Visible = True
  HBox1.Visible = True
  TextBoxSearch_Change
  
  ButtonAbout.Text = "&About"
Endif

End

Public Sub ViewFiles(bMode As Boolean)
Dim iCount, iRow As Integer

If bExt Then 
  LabelExt.Text = "Showing only files matching choosen extensions"
Else
    LabelExt.Text = ""
End If

If bMode Then
  ButtonView.Tag = 1
  ButtonView.Text = "&Close"
  HBoxDir.Visible = False
  VBoxView.Visible = True
  HBoxCheckBoxes.Visible = False
  ButtonAbout.Enabled = False
  ButtonSearch.Enabled = False
  HBox1.Enabled = False
  GridViewFiles.Rows.Height = 18
  GridViewFiles.Columns.count = 1
  GridViewFiles.Rows.Count = sFileStore.count
  For icount = 0 To sFileStore.Max
    GridViewFiles[iRow, 0].text = sFileStore[iCount]
    Inc iRow
  Next
Else
  ButtonView.Tag = 0
  ButtonView.Text = "&View"
  HBoxDir.Visible = True
  VBoxView.Visible = False
  HBoxCheckBoxes.Visible = True
  ButtonAbout.Enabled = True
  TextBoxSearch_Change
  HBox1.Enabled = True
Endif

End

Public Sub AllCheckboxes_Click()

If CheckBoxHidden.value = True Then 
  DirChooserFF.ShowHidden = True
Else
  DirChooserFF.ShowHidden = False
End If

DirChooserFF_Change

End

Public Sub Form_KeyPress()

If ButtonSearch.Enabled = True And ButtonSearch.Visible = True Then
  If Key.code = Key.Return Or Key.code = Key.Enter Then TimerSearch.Trigger
Endif

End

Public Sub TimerSearch_Timer()

ButtonSearch.Value = True

End

Public Sub Form_Close()

If Not bExtOpen Then Settings.Write(Me, "Window")                                                  'Store the window position and size

End

Public Sub FormatTime(tStart As Date) As String
Dim sMin, sSec, sMs As String

tStart = Time(Now) - tStart

If Val(Format(tStart, "nn")) > 0 Then sMin = Format(tStart, "n") & " min "
If Val(Format(tStart, "ss")) > 0 Then sSec = Format(tStart, "s") & " sec "
If Not sSec Then
  If Val(Format(tStart, "uu")) > 0 Then sMs = Format(tStart, "u") & " ms"
End If 

Return "Search time: - " & sMin & sSec & sMs

End

Public Sub GridViewFiles_Click()
Dim sFolder As String = DirChooserFF.SelectedPath

Desktop.Open(sFolder &/ GridViewFiles[Last.Row, Last.Column].Text)

End
